/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

@App:name("telemetry-app")
@App:description("Cellery Telemetry app for processing and storing http requests")

@source(type="inMemory", topic="istio-mixer-report", @map(type="passThrough"))
define stream TelemetryStreamIn(sourceNamespace string, sourceInstance string, sourceInstanceKind string,
                                sourceComponent string, sourcePod string, destinationNamespace string,
                                destinationInstance string, destinationInstanceKind string,
                                destinationComponent string, destinationPod string, contextReporterKind string,
                                requestHeadersMap object, requestPath string, requestMethod string,
                                requestSizeBytes long, responseCode long, responseDurationNanoSec int,
                                responseSizeBytes long);

define stream ProcessedRequestsStream(sourceInstance string, sourceInstanceKind string, sourceComponent string,
                                      destinationInstance string, destinationInstanceKind string,
                                      destinationComponent string, httpResponseGroup string,
                                      responseTimeMilliSec double, requestSizeBytes long, responseSizeBytes long);

@store(type="rdbms", datasource="CELLERY_OBSERVABILITY_DB", field.length="sourceInstance:253, sourceInstanceKind:9, sourceComponent:253, destinationInstance:253, destinationInstanceKind:9, destinationComponent: 253, httpResponseGroup:3")
@purge(enable="false")
define aggregation RequestAggregation from ProcessedRequestsStream
select
    sourceInstance,
    sourceInstanceKind,
    sourceComponent,
    destinationInstance,
    destinationInstanceKind,
    destinationComponent,
    httpResponseGroup,
    sum(responseTimeMilliSec) as totalResponseTimeMilliSec,
    sum(requestSizeBytes) as totalRequestSizeBytes,
    sum(responseSizeBytes) as totalResponseSizeBytes,
    count() as requestCount
group by
    sourceInstance, sourceComponent, destinationInstance, destinationComponent, httpResponseGroup
aggregate every sec...year;

--
-- Storing the data received from Istio Telemetry
--

from TelemetryStreamIn
select
    sourceInstance,
    sourceInstanceKind,
    sourceComponent,
    destinationInstance,
    destinationInstanceKind,
    destinationComponent,
    contextReporterKind,
    cast(map:get(requestHeadersMap, "x-request-id"), "string") as requestId,
    str:concat(convert(cast(responseCode, "int") / 100, "string"), "xx") as httpResponseGroup,
    convert(responseDurationNanoSec, "double") / 1000000 as responseTimeMilliSec,
    ifThenElse(requestSizeBytes is null, 0L, requestSizeBytes) as requestSizeBytes,
    ifThenElse(responseSizeBytes is null, 0L, responseSizeBytes) as responseSizeBytes
insert into ProcessedTelemetryStream;

from every(e1=ProcessedTelemetryStream) ->
    not ProcessedTelemetryStream[e1.requestId == e2.requestId] for 1 minute
    or e2=ProcessedTelemetryStream[e1.requestId == e2.requestId] within 1 minute
select
    e1.sourceInstance,
    e1.sourceInstanceKind,
    e1.sourceComponent,
    e1.destinationInstance,
    e1.destinationInstanceKind,
    e1.destinationComponent,
    ifThenElse(e1.httpResponseGroup is null, e2.httpResponseGroup, e1.httpResponseGroup) as httpResponseGroup,
    ifThenElse(e1.responseTimeMilliSec is null, e2.responseTimeMilliSec, e1.responseTimeMilliSec) as responseTimeMilliSec,
    ifThenElse(e1.requestSizeBytes is null, e2.requestSizeBytes, e1.requestSizeBytes) as requestSizeBytes,
    ifThenElse(e1.responseSizeBytes is null, e2.responseSizeBytes, e1.responseSizeBytes) as responseSizeBytes
insert into SanitizedRequestsStream;

from SanitizedRequestsStream[(not httpResponseGroup is null) and (not responseTimeMilliSec is null)
                             and (not requestSizeBytes is null) and (not responseSizeBytes is null)]
insert into ProcessedRequestsStream;
