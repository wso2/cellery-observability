/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

@App:name("istio-telemetry-app")
@App:description("This receives the attributes generally received by the Istio Mixer")

@source(type="runtime-agent", port="9091", @map(type="json", fail.on.missing.attribute="false"))
define stream TelemetryStream(requestID string, traceID string, spanID string, sourceUID string, sourceInstance string,
                              sourceInstanceKind string, sourceComponent string, destinationUID string,
                              destinationInstance string, destinationInstanceKind string, destinationComponent string,
                              contextReporterKind string, requestPath string, requestMethod string,
                              requestTotalSize long, responseCode long, responseDurationNanoSec long,
                              responseTotalSize long);

@sink(type="inMemory", topic="istio-mixer-report", @map(type="passThrough"))
define stream TelemetryInMemorySink(requestId string, traceId string, spanId string, sourceNamespace string,
                                    sourceInstance string, sourceInstanceKind string, sourceComponent string,
                                    sourcePod string, destinationNamespace string, destinationInstance string,
                                    destinationInstanceKind string, destinationComponent string, destinationPod string,
                                    contextReporterKind string, requestPath string, requestMethod string,
                                    requestSizeBytes long, responseCode long, responseDurationNanoSec long,
                                    responseSizeBytes long);

define function extractFromUID[javascript] return string {
    var uid = data[0];
    var index = data[1];    // 1: pod, 2: namespace

    var matches = /^([a-z0-9-.]+)\.([a-z0-9-]+)$/.exec(uid);

    var extractedData = "";
    if (matches) {
        extractedData = matches[index];
    } else if (uid === "unknown") {
        extractedData = "";
    } else {
        if (index === 1) {
            extractedData = uid;
        } else {
            extractedData = "";
        }
    }
    return extractedData;
};

from TelemetryStream[(not sourceUID is null) and (not destinationUID is null)]
select
    requestID as requestId,
    traceID as traceId,
    spanID as spanId,
    str:replaceFirst(sourceUID, "^kubernetes://", "") as sourceUID,
    sourceInstance,
    sourceInstanceKind,
    sourceComponent,
    str:replaceFirst(destinationUID, "^kubernetes://", "") as destinationUID,
    destinationInstance,
    destinationInstanceKind,
    destinationComponent,
    contextReporterKind,
    requestPath,
    requestMethod,
    requestTotalSize as requestSizeBytes,
    responseCode,
    responseDurationNanoSec,
    responseTotalSize as responseSizeBytes
insert into PreprocessedTelemetryStream;

-- Storing the required attributes in a in memory sink (To be accessed from other siddhi apps)
-- When updating the used attributes from the received attributes the stream definitions needs to be updated in all
-- the places where the in memory source is used

from PreprocessedTelemetryStream
select
    requestId,
    traceId,
    spanId,
    extractFromUID(sourceUID, 2) as sourceNamespace,
    sourceInstance,
    sourceInstanceKind,
    sourceComponent,
    extractFromUID(sourceUID, 1) as sourcePod,
    extractFromUID(destinationUID, 2) as destinationNamespace,
    destinationInstance,
    destinationInstanceKind,
    destinationComponent,
    extractFromUID(destinationUID, 1) as destinationPod,
    contextReporterKind,
    requestPath,
    requestMethod,
    requestSizeBytes,
    responseCode,
    responseDurationNanoSec,
    responseSizeBytes
insert into TelemetryInMemorySink;
